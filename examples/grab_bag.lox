// Built-ins
let empty = List.empty;
let first = List.first;
let rest = List.rest;

//
// Lists
//
let friends = ["alice", "bob"];
let family = ["charlie", "devin"];
let people = ["joe", ..friends, "john", ..family];
print rest(people);

//
// Records
//
let boss = {
    name: "Bob Vance",
    company: "Vance Refrigeration",
};
let updated = {..boss, line_of_work: .Refrigeration};
print boss;
print updated.company;
let {
  name,
  line_of_work: subtitle,
} = updated;
print name;
print subtitle;

//
// Variants
//
let green = .Green;
let red = .Red;
let either = true ? green : red;
print match either {
    .Green -> 0,
    .Red -> 1,
};

let send_email = \x {
  if x == "a" then return .MissingSubject;
  if x == "b" then return .NetworkError(123);
  if x == "c" then return .OOM;
  return .Sent;
};
let result = match send_email("a") {
  .Sent           -> .Alert("success"),
  .MissingSubject -> .Alert("missing subject line"),
  something_bad   -> something_bad,
};

//
// Functions
//
let sub = \x, y -> x - y;
print sub(7, 2);

let one_minus_x = sub(1, _);
let x_minus_one = sub(_, 1);
print one_minus_x(3);
print x_minus_one(3);

let numbers = [1,2,3];
print numbers \> first \> sub(_, 1);

let make_user = \data {
    if data.name == "null" then {
        print "hmm";
        return .Anonymous;
    }
    let random_id = 123;
    return .User({
        user_id: random_id,
        name: data.name,
        birth_year: data.birth_year,
        age: \{as_of_year: current_year} -> current_year - data.birth_year,
    });
};
let user = make_user({name: "Bob", birth_year: 1974});
print match user {
    .User(u) -> u.age({as_of_year: 2025}),
    .Anonymous -> 0,
};